plugins {
    id 'java-library'
    id 'io.spring.dependency-management'
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom optionalImplementation, implementation
    integrationTestRuntimeOnly.extendsFrom optionalRuntimeOnly, runtimeOnly
}

java {
    sourceCompatibility = '21'

    registerFeature('optional') {
        usingSourceSet(sourceSets.main)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation platform(libs.cloudevents.bom)
}

dependencyManagement {
    imports {
        mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test

    useJUnitPlatform()

    testLogging {
        events "passed"
    }
}
check.dependsOn integrationTest
